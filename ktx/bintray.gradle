apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

publishing {
    publications {
        Publication(MavenPublication) {
            artifact("$buildDir/outputs/aar/ktx-release.aar")
            groupId 'co.infinum'
            artifact sourcesJar
            artifact javadocsJar
            artifactId 'goldfinger-ktx'
            version versions.goldfinger

            pom.withXml {
                def root = asNode()
                def dependenciesNode = root.appendNode('dependencies')

                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    if (it.name == 'core') {
                        dependencyNode.appendNode('groupId', 'co.infinum')
                        dependencyNode.appendNode('artifactId', 'goldfinger')
                        dependencyNode.appendNode('version', versions.goldfinger)
                    } else {
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publish = false
    publications = ['Publication']

    pkg {
        repo = 'android'
        name = 'goldfinger-ktx'
        userOrg = 'infinum'
        publicDownloadNumbers = true

        version {
            name = versions.goldfinger
        }
    }
}